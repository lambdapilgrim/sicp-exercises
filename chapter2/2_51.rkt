#! /usr/bin/env racket
#lang racket

(define (make-vect xcor ycor)
  (cons xcor ycor))

(define (xcor-vect vect)
  (car vect))

(define (ycor-vect vect)
  (cdr vect))

(define (add-vect vect1 vect2)
  (make-vect
   (+ (xcor-vect vect1) (xcor-vect vect2))
   (+ (ycor-vect vect1) (ycor-vect vect2))))

(define (sub-vect vect1 vect2)
  (make-vect
   (- (xcor-vect vect1) (xcor-vect vect2))
   (- (ycor-vect vect1) (ycor-vect vect2))))

(define (scale-vect factor vect)
  (make-vect
   (* (xcor-vect vect) factor)
   (* (ycor-vect vect) factor)))

(define (make-frame origin edge1 edge2)
  (list origin edge1 edge2))

(define (origin-frame frame)
  (car frame))

(define (edge1-frame frame)
  (car (cdr frame)))

(define (edge2-frame frame)
  (car (cdr (cdr frame))))

(define (frame-coord-map frame)
  (lambda (v)
    (add-vect
     (origin-frame frame)
     (add-vect (scale-vect (xcor-vect v)
                           (edge1-frame frame))
               (scale-vect (ycor-vect v)
                           (edge2-frame frame))))))

(define (transform-painter painter origin corner1 corner2)
  (lambda (frame)
    (let ((m (frame-coord-map frame)))
      (let ((new-origin (m origin)))
        (painter
         (make-frame new-origin
                     (sub-vect (m corner1) new-origin)
                     (sub-vect (m corner2) new-origin)))))))

(define (rotate90 painter)
  (transform-painter painter
                     (make-vect 1.0 0.0)
                     (make-vect 1.0 1.0)
                     (make-vect 0.0 0.0)))

(define (rotate270 painter)
  (transform-painter painter
                     (make-vect 0.0 1.0)
                     (make-vect 0.0 0.0)
                     (make-vect 1.0 1.0)))

(define (beside painter1 painter2)
  (let ((split-point (make-vect 0.5 0.0)))
    (let ((paint-left
           (transform-painter painter1
                              (make-vect 0.0 0.0)
                              split-point
                              (make-vect 0.0 1.0)))
          (paint-right
           (transform-painter painter2
                              split-point
                              (make-vect 1.0 0.0)
                              (make-vect 0.5 1.0))))
      (lambda (frame)
        (paint-left frame)
        (paint-right frame)))))

(define (below-1 painter1 painter2)
  (let ((split-point (make-vect 0.0 0.5)))
    (let ((above-painter
          (transform-painter painter2
                             split-point
                             (make-vect 1.0 0.5)
                             (make-vect 0.0 1.0)))
          (below-painter
           (transform-painter painter1
                              (make-vect 0.0 0.0)
                              (make-vect 1.0 0.0)
                              split-point)))
      (lambda (frame) (above-painter frame) (below-painter frame)))))

(define (test-painter frame)
  (newline)
  (display "origin ")(display (origin-frame frame))
  (newline)
  (display "edge1 ")(display (edge1-frame frame))
  (newline)
  (display "edge2 ")(display (edge2-frame frame))
  (newline))

(define test-frame (make-frame
                    (make-vect 5.0 4.0)
                    (make-vect 1.0 -1.0)
                    (make-vect 1.0 1.0)))

((below-1 test-painter test-painter) test-frame)

;origin (5.5 . 4.5)
;edge1 (1.0 . -1.0)
;edge2 (0.5 . 0.5)

;origin (5.0 . 4.0)
;edge1 (1.0 . -1.0)
;edge2 (0.5 . 0.5)


(define (below-2 painter1 painter2)
  (rotate90 (beside (rotate270 painter1) (rotate270 painter2))))

((below-2 test-painter test-painter) test-frame)

;origin (5.0 . 4.0)
;edge1 (1.0 . -1.0)
;edge2 (0.5 . 0.5)

;origin (5.5 . 4.5)
;edge1 (1.0 . -1.0)
;edge2 (0.5 . 0.5)

;I used rotate270 to first rotate both pictures by 90 degrees to the right, then
;fed them to beside. Then I rotated the whole painter (generated by beside), by
;90 degrees to the left.
